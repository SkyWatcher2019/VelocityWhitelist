plugins {
  id 'java'
  id 'org.jetbrains.gradle.plugin.idea-ext' version '1.0.1'
  id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'ru.rioworlds.velocitywhitelist'
version = '1.0-SNAPSHOT'

compileJava {
  getOptions().setEncoding('UTF-8')
}

java {
  sourceCompatibility = '17'
  targetCompatibility = '17'
}

repositories {
  mavenCentral()

  maven {
    name = 'elytrium-repo'
    url = 'https://maven.elytrium.net/repo/'
  }

  maven {
    name = 'papermc-repo'
    url = 'https://repo.papermc.io/repository/maven-public/'
  }
}

dependencies {
  compileOnly 'com.velocitypowered:velocity-api:3.1.2-SNAPSHOT'
  annotationProcessor 'com.velocitypowered:velocity-api:3.1.2-SNAPSHOT'
  implementation 'net.elytrium:elytrium-java-commons:1.0.5'
}

shadowJar {
  getArchiveClassifier().set("")
}

sourceSets.main.getJava().srcDir(getTasks().register("generateTemplates", Copy) { task ->
  task.getInputs().properties("version": getVersion())

  task.from(file("src/main/templates"))
      .into(getLayout().getBuildDirectory().dir("generated/sources/templates"))
      .expand("version": getVersion())
}.map {
  it.getOutputs()
})

assemble.dependsOn(shadowJar)